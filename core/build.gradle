apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'maven-publish'


android {
    namespace 'org.futo.circles.core'
    compileSdk rootProject.ext.sdk_version

    defaultConfig {
        minSdk rootProject.ext.min_sdk_version
        targetSdk rootProject.ext.sdk_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.FlowPreview",
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.11.0'
    api 'androidx.recyclerview:recyclerview:1.3.2'
    api "androidx.autofill:autofill:1.1.0"

    //Kotlin
    api "androidx.core:core-ktx:1.12.0"

    //androidx lifecycle
    def lifecycle_version = "2.7.0"
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    //androidx navigation
    api "androidx.navigation:navigation-fragment-ktx:$rootProject.ext.androidx_nav_version"
    api "androidx.navigation:navigation-ui-ktx:$rootProject.ext.androidx_nav_version"

    //ViewBinding
    api 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.9'

    //Hilt
    implementation "com.google.dagger:hilt-android:$rootProject.ext.hilt_version"
    kapt "com.google.dagger:hilt-compiler:$rootProject.ext.hilt_version"

    //Matrix
    api('org.futo.gitlab.circles:matrix-android-sdk:v1.6.10.30@aar') {
        transitive = true
    }

    //Retrofit2
    def retrofit_version = '2.10.0'
    api "com.squareup.retrofit2:retrofit:$retrofit_version"
    api "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //Gson
    api 'com.google.code.gson:gson:2.10.1'

    //Worker
    def work_version = "2.9.0"
    api "androidx.work:work-runtime-ktx:$work_version"

    //Glide
    def glide_version = '4.16.0'
    api "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation 'jp.wasabeef:glide-transformations:4.3.0'

    //Webp animations
    //noinspection GradleDependency
    api 'com.github.penfeizhou.android.animation:glide-plugin:2.28.0'

    //QR
    api 'com.google.zxing:core:3.5.3'

    //Markdown
    def markwon_version = "4.6.2"
    api "io.noties.markwon:core:$markwon_version"
    api "io.noties.markwon:linkify:$markwon_version"
    api "io.noties.markwon:ext-strikethrough:$markwon_version"
    api "io.noties.markwon:ext-tasklist:$markwon_version"
    api 'io.element.android:wysiwyg:2.34.0'

    //Passkeys
    api "androidx.credentials:credentials:1.3.0-alpha01"
    api "androidx.credentials:credentials-play-services-auth:1.3.0-alpha01"

    //ExoPlayer
    def exoplayer_version = '1.3.0'
    api "androidx.media3:media3-exoplayer:$exoplayer_version"
    api "androidx.media3:media3-ui:$exoplayer_version"

    //profile placeholder
    implementation 'com.github.WycliffeAssociates:jdenticon-kotlin:1.1'
    implementation 'com.caverock:androidsvg-aar:1.4'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

kapt {
    correctErrorTypes = true
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = rootProject.ext.modules_groupId
            artifactId = "core"
            version rootProject.ext.modules_version

            afterEvaluate {
                from components.release
            }
        }
    }
}