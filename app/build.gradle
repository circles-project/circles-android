apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'

android {
    compileSdk rootProject.ext.sdkVersion

    defaultConfig {
        applicationId "org.futo.circles"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.sdkVersion
        versionCode 11
        versionName "1.0.10"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cFlags += "-std=c11"
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "String", "US_SERVER_DOMAIN", "\"nl.circles-dev.net\""
            buildConfigField "String", "EU_SERVER_DOMAIN", "\"matrix.org\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "US_SERVER_DOMAIN", "\"circu.li\""
            buildConfigField "String", "EU_SERVER_DOMAIN", "\"eu.circu.li\""
        }
    }

    def flavor_dimension_name = "store"
    flavorDimensions flavor_dimension_name
    productFlavors {
        gplay {
            dimension flavor_dimension_name
            buildConfigField("boolean", "IS_SUBSCRIPTIONS_ENABLED", "false")
        }
        fdroid {
            dimension flavor_dimension_name
            buildConfigField("boolean", "IS_SUBSCRIPTIONS_ENABLED", "false")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.FlowPreview",
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }
    namespace 'org.futo.circles'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.autofill:autofill:1.1.0"

    //Matrix
    implementation 'org.matrix.android:matrix-sdk-android:1.5.30.5'

    //Kotlin
    implementation "androidx.core:core-ktx:1.10.0"

    //androidx lifecycle
    def lifecycle_version = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    //androidx navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.ext.androidx_nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.ext.androidx_nav_version"

    //Koin
    implementation "io.insert-koin:koin-core:3.2.2"
    implementation "io.insert-koin:koin-android:3.3.0"

    //ViewBinding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.6'

    //Glide
    def glide_version = '4.14.2'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    //TextDrawable
    implementation 'com.alvinhkh:TextDrawable:c1c2b5b'

    //ChromeTabs
    implementation "androidx.browser:browser:1.5.0"

    //ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.18.6'

    //Image zoom
    implementation 'com.jsibbold:zoomage:1.3.1'

    //Firebase
    gplayImplementation platform('com.google.firebase:firebase-bom:31.1.1')
    gplayImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    gplayImplementation 'com.google.firebase:firebase-analytics-ktx'
    gplayImplementation 'com.google.firebase:firebase-messaging-ktx'

    // UnifiedPush
    implementation 'com.github.UnifiedPush:android-connector:2.1.1'

    //Subscriptions
    gplayImplementation 'com.android.billingclient:billing-ktx:5.2.0'

    //Emoji
    implementation "com.vanniktech:emoji-google:0.15.0"

    //Retrofit2
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //Gson
    implementation 'com.google.code.gson:gson:2.10'

    //QR
    implementation 'com.google.zxing:core:3.5.1'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.3.2'

    //Password strength
    implementation 'com.nulab-inc:zxcvbn:1.7.0'

    //Webp animations
    implementation 'com.github.penfeizhou.android.animation:glide-plugin:2.24.0'

    // Custom shadow
    implementation 'com.github.BluRe-CN:ComplexView:1.1'

    //Markdown
    def markwon_version = "4.6.2"
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:ext-tasklist:$markwon_version"

    //Mentions autocomplete
    implementation "com.otaliastudios:autocomplete:1.1.0"

    //Shake detection
    implementation 'com.squareup:seismic:1.0.3'

    //Worker
    def work_version = "2.8.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    //Coroutines
    def coroutines_version = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    //test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains("gplay")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}