apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'


android {
    compileSdk rootProject.ext.sdkVersion

    defaultConfig {
        applicationId "org.futo.circles"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            resValue "string", "app_name", "Circuli Stage"
            buildConfigField "String", "US_SERVER_DOMAIN", "\"kombucha.social\""
            buildConfigField "String", "EU_SERVER_DOMAIN", "\"eu.kombucha.social\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Circuli"
            buildConfigField "String", "US_SERVER_DOMAIN", "\"circu.li\""
            buildConfigField "String", "EU_SERVER_DOMAIN", "\"eu.circu.li\""
        }
    }

    def flavor_dimension_name = "store"
    flavorDimensions flavor_dimension_name
    productFlavors {
        gplay {
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
            afterEvaluate {
                tasks.matching { it.name.contains("GoogleServices") && !it.name.contains("gplay") }*.enabled = false
            }
            dimension flavor_dimension_name
            buildConfigField("boolean", "IS_SUBSCRIPTIONS_ENABLED", "true")
        }
        fdroid {
            dimension flavor_dimension_name
            buildConfigField("boolean", "IS_SUBSCRIPTIONS_ENABLED", "false")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.FlowPreview",
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Matrix
    implementation 'org.matrix.android:matrix-sdk-android:1.4.17'

    //Kotlin
    implementation "androidx.core:core-ktx:1.8.0"

    //Subscriptions
    implementation 'com.android.billingclient:billing-ktx:5.0.0'

    //androidx
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.ext.androidx_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.ext.androidx_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.ext.androidx_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.ext.androidx_version"

    //Koin
    def koin_version = "3.2.0"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    //ViewBinding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.6'

    //Glide
    def glide_version = "4.13.2"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    //TextDrawable
    implementation 'com.alvinhkh:TextDrawable:c1c2b5b'

    //Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //ChromeTabs
    implementation "androidx.browser:browser:1.4.0"

    //ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.18.1'

    //Firebase crash reports for gplay flavor
    gplayImplementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.12'
    gplayImplementation 'com.google.firebase:firebase-analytics-ktx:21.1.0'

    //test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}