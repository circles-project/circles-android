apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'

android {
    compileSdk rootProject.ext.sdkVersion

    defaultConfig {
        applicationId "org.futo.circles"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.sdkVersion
        versionCode 4
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cFlags += "-std=c11"
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            resValue "string", "app_name", "Circuli Stage"
            buildConfigField "String", "US_SERVER_DOMAIN", "\"nl.circles-dev.net\""
            buildConfigField "String", "EU_SERVER_DOMAIN", "\"eu.kombucha.social\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Circuli"
            buildConfigField "String", "US_SERVER_DOMAIN", "\"circu.li\""
            buildConfigField "String", "EU_SERVER_DOMAIN", "\"eu.circu.li\""
        }
    }

    def flavor_dimension_name = "store"
    flavorDimensions flavor_dimension_name
    productFlavors {
        gplay {
            dimension flavor_dimension_name
            buildConfigField("boolean", "IS_SUBSCRIPTIONS_ENABLED", "true")
        }
        fdroid {
            dimension flavor_dimension_name
            buildConfigField("boolean", "IS_SUBSCRIPTIONS_ENABLED", "false")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.FlowPreview",
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Matrix
    implementation 'org.matrix.android:matrix-sdk-android:1.5.2'

    //Kotlin
    implementation "androidx.core:core-ktx:1.9.0"

    //androidx lifecycle
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    //androidx navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.ext.androidx_nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.ext.androidx_nav_version"

    //Koin
    def koin_version = "3.2.0"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    //ViewBinding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.6'

    //Glide
    def glide_version = "4.13.2"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    //TextDrawable
    implementation 'com.alvinhkh:TextDrawable:c1c2b5b'

    //ChromeTabs
    implementation "androidx.browser:browser:1.4.0"

    //ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.18.1'

    //Firebase crash reports
    gplayImplementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.13'
    gplayImplementation 'com.google.firebase:firebase-analytics-ktx:21.1.1'

    //Subscriptions
    gplayImplementation 'com.android.billingclient:billing-ktx:5.0.0'

    //Emoji
    implementation "com.vanniktech:emoji-google:0.15.0"

    //Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    //test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains("gplay")){
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}