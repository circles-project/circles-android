apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'androidx.navigation.safeargs.kotlin'


android {
    compileSdk rootProject.ext.sdkVersion

    defaultConfig {
        applicationId "com.futo.circles"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    def flavor_dimension_name = "version"
    flavorDimensions flavor_dimension_name
    productFlavors {
        dev {
            dimension flavor_dimension_name
            applicationIdSuffix ".stage"
            resValue "string", "app_name", "Circles Stage"
            buildConfigField("String", "MATRIX_HOME_SERVER_URL", "\"https://matrix.eu.kombucha.social/\"")
        }
        prod {
            dimension flavor_dimension_name
            resValue "string", "app_name", "Circles"
            buildConfigField("String", "MATRIX_HOME_SERVER_URL", "\"https://matrix.kombucha.social/\"")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    //Kotlin
    implementation "androidx.core:core-ktx:1.7.0"

    //Lifecycle
    def lifecycle_version = "2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    //Koin
    def koin_version = "3.1.5"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:29.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.ext.nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.ext.nav_version"

    //ViewBinding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.6'

    //Matrix
    implementation 'org.matrix.android:matrix-android-sdk2:1.3.18'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    //Glide
    def glide_version = "4.13.1"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    //test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}